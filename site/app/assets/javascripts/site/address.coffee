$ = jQuery

$ ->
  $.fn.addressForm = (opts) ->
    billing_selector_for = (name) ->
      billing_el('select',name)

    billing_input_for = (name) ->
      billing_el('input',name)

    billing_el = (type,name) ->
      $("#{type}[name$='[aria_billing_info][#{name}]']")

    billing_label_for = (el) ->
      el.closest('.control-group').find('.control-label')

    # Store our selections for easier use
    country_select   = billing_selector_for('country')
    region_select    = billing_selector_for('region')

    # Get the city/region/zip label
    region_label = billing_label_for(region_select)
    # Split the label into a list
    label_ul     = $('<ul/>')
    label_items  = region_label.text().split(', ')
    $.each(label_items, (i,val) ->
      $('<li/>')
        .text(val)
        .addClass(val.toLowerCase())
        .appendTo(label_ul)
    )
    region_label.html(label_ul)

    # Save the default value
    region_select_default = region_select.val()
    # Remove our empty placeholder
    region_select.find('option:empty').detach()
    # Store the region optgroups, this gets destroyed when we detach on change
    groups = region_select.html()

    country_select.on 'change', (ev) ->
      args = arguments[1] || {}

      # Find the selected country
      selected = $(this).find("option:selected")
      name = selected.text()
      code = selected.val()

      # Get options for the country
      subdivision = selected.attr('data-subdivision')
      postal_code = selected.attr('data-postal_code')

      # Replace the groups with our stored versions
      region_select.html(groups)
      # Remove any unwanted groups
      region_select.find("optgroup[label!='#{name}']").detach()
      # Promote the options so we don't see the optgroup
      region_select.html(region_select.find('option'))
      # Add a "default" value to the region select
      placeholder = $("<option value=''>Select a #{subdivision.toLowerCase()}</option>")
      unless args.first_run
        placeholder.attr('selected','selected')
      region_select.prepend(placeholder)

      # Preserve the selected option generated by the page, if possible
      if args.first_run
        region_select.val(region_select_default || "")
      else
        region_select.val("")

      # Change the state and postal code in the label
      region_label = billing_label_for(region_select)
      region_label.find('ul li.state').html(subdivision)
      region_label.find('ul li.zip').html(postal_code)
      billing_input_for('zip').attr('placeholder',postal_code)

      # Show/hide VAT row
      if (opts.vatCountries)
        if opts.vatCountries.indexOf(code) >= 0
          $('.tax_nonvat').hide()
          $('.tax_vat').show()
        else
          $('.tax_vat').hide().find('input').val('')
          $('.tax_nonvat').show()

    # Update the form based on the current value
    country_select.trigger 'change', {first_run: true}
